plugins {
    id "com.gradle.enterprise" version "3.15.1"
    id "io.spring.ge.conventions" version "0.0.15"
}

include "dhmp-security-core"
include "dhmp-security-oauth2-authorization-server"
include "dhmp-security-oauth2-resource-server"
include "security-bom"
include "security-parent"

rootProject.name = "dhmp-security"
rootProject.children.each { project ->
    project.buildFileName = "${project.name}.gradle"
}

settings.gradle.projectsLoaded {
    gradleEnterprise {
        buildScan {
            File buildDir = settings.gradle.rootProject.getBuildDir()
            buildDir.mkdirs()
            new File(buildDir, "build-scan-uri.txt").text = "(build scan not generated)"
            buildScanPublished { scan ->
                if (buildDir.exists()) {
                    new File(buildDir, "build-scan-uri.txt").text = "${scan.buildScanUri}\n"
                }
            }
        }
    }
}
